*
sysonly
$ifnull @1
   $write
   $write OPERATOR COMMANDS ARE:
   $write
   $write FTP ON
   $write FTP OFF
   $write FTP RESET
   $write FTP USERS
   $write
   $write ALL OTHER COMMANDS ARE RESERVED FOR THE FTP SERVER.
   $write
   $exit
$endc
*
xdelete users.ftp,reset.ftp
xdelete off.ftp,down.ftp,stop.ftp
xdelete on.ftp,up.ftp,start.ftp
xdelete copyout.ftp,copyin.ftp
xdelete delete.ftp,rename.ftp
xdelete dir.ftp,checkvol.ftp
*
*** FTP USERS ************************************************
xallocate users.ftp,index,80
$ifx @1.ftp
   $write SHOW FTP USERS
   delete @1.ftp
   $exit
$endc
delete users.ftp
*
*** FTP OFF **************************************************
xallocate off.ftp,index,80
xallocate down.ftp,index,80
xallocate stop.ftp,index,80
$ifx @1.ftp
   $write STOP FTP SERVER
   delete off.ftp,down.ftp,stop.ftp
   $exit
$endc
delete off.ftp,down.ftp,stop.ftp
*
*** FTP ON ***************************************************
xallocate on.ftp,index,80
xallocate up.ftp,index,80
xallocate start.ftp,index,80
$ifx @1.ftp
   $write START FTP SERVER
   delete on.ftp,up.ftp,start.ftp
   $exit
$endc
delete on.ftp,up.ftp,start.ftp
*
*** FTP RESET ************************************************
xallocate reset.ftp,index,80
$ifx @1.ftp
   $write SHOW FTP USERS
   $write STOP FTP SERVER
   $write START FTP SERVER
   delete reset.ftp
   $exit
$endc
delete reset.ftp
*
*** DIR ******************************************************
xallocate dir.ftp,index,80
$ifx @1.ftp
   delete @1.ftp
   $write === BEGIN DIR @2 ===
   dir @2
   $write === END DIR @2 ===
   $exit
$endc
delete dir.ftp
*
*** COPYOUT **************************************************
xallocate copyout.ftp,index,80
$ifx @1.ftp
   delete @1.ftp
   $write === BEGIN COPYOUT @2 ===
   type @2
   $write === END COPYOUT @2 ===
   $exit
$endc
delete copyout.ftp
*
*** COPYIN ***************************************************
xallocate copyin.ftp,index,80
$ifx @1.ftp
   delete @1.ftp
   $write === BEGIN COPYIN @2 ===
   $job
      xallocate copyin.tmp,index,@3
      xdelete @2
   $termjob
   $ife 0
      $build edit.tmp
         option command=con: ; get copyin.tmp ; append
      $endb
      $job
         load .bg,edit32
         task .bg
         start ,command=edit.tmp,list=con:
      $termjob
      $ife 0
         copy copyin.tmp,@2
      $endc
      xdelete edit.tmp
   $endc
   xdelete copyin.tmp
   $write === END COPYIN @2 ===
   $exit
$endc
delete copyin.ftp
*** STOR (COPYIN) ********************************************
xallocate stor.ftp,index,80
$ifx @1.ftp
   delete @1.ftp
   $ifx @2
      $write STOR EXISTS
   $else
      $write STOR NEWFILE
   $endc
   $exit
$endc
delete stor.ftp
*
*** CHECKVOL *************************************************
xallocate checkvol.ftp,index,80
$ifx @1.ftp
   delete @1.ftp
   $write === BEGIN CHECKVOL @2 ===
   $job
      display volume ,@2
      $write VOLUME @2 OK
   $termjob
   $ifne 0
      $write VOLUME @2 NOT FOUND
   $endc
   $write === END CHECKVOL @2 ===
   $exit
$endc
delete checkvol.ftp
*
*** DELETE ***************************************************
xallocate delete.ftp,index,80
$ifx @1.ftp
   delete @1.ftp
   $write === BEGIN DELETE @2 ===
   $job
      delete @2
   $termjob
   $ife 0
      $write FILE DELETED
   $endc
   $write === END DELETE @2 ===
   $exit
$endc
delete delete.ftp
*
*** RENAME ***************************************************
xallocate rename.ftp,index,80
$ifx @1.ftp
   delete @1.ftp
   $write === BEGIN RENAME @2 @3 ===
   $job
      rename @2,@3
   $termjob
   $ife 0
      $write FILE RENAMED
   $endc
   $write === END RENAME @2 @3 ===
   $exit
$endc
delete rename.ftp
*
$write FTP UNRECOGNISED COMMAND @1
$exit
*
